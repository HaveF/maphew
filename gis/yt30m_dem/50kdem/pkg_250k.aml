&severity &error &routine bail
/* if there are commandline arguments, make them the TODO list
&args dest_dir quad:REST
&ty --------------------------------------------------------------------------
&ty   * [upcase %aml$file%] * Project: Updated LIMS 50k DEM * mhw * 15-Nov-2k
&ty     ::Step 11.5::  (see scripts\__ReadMe.aml)
&ty
&ty     - Package 250k DEMs for distribution
&ty     * GNU Tar must be available on system *
&ty
&ty     Autorun: &r %aml$file% {destination_dir} {quad}
&ty --------------------------------------------------------------------------
&ty
&ty     Current commandline: &r %aml$file% %dest_dir% %quad%
&ty
&if not [variable .library] &then &return &error Project Environment is not setup. Look for ./scripts/setenv.aml

&if [null %quad%] &then &do
    &s pause [response '<Enter> to continue, something else to abort']
    &if not [null %pause%] &then &return %aml$file% exited by user request.
    &end

&workspace %.home%

/* put header variables and other semi-global code here
&set dest_dir = %.home%\release ; &sys md %dest_dir% > nul
&s nix_dir [subst %dest_dir% \ /]; &s nix_dir [subst %nix_dir% :]; &s nix_dir //%nix_dir%
&set dem = dem_90m
&set shade = shade_90m

/* check for quad commandline args
&if [null %quad%] &then &set quad = [listfile * -directory]

&do quad &list [unquote %quad%]
  &workspace %quad%; &type ...entering %quad%

      /* &set tile_list = [listfile * -workspace]
      &CALL MAIN

  &workspace  ..
&end

&if [locase[show program]] = grid &then QUIT /* exit GRID

&RETURN ...[upcase %aml$fullfile%] finished normally.

/*============================================================================
&ROUTINE MAIN
   /*-------mark workspace busy----------------------
   &if [exists lock -file] &then &return
   &set lockfile = [OPEN lock openstat -write]
   &if  %openstat% = 0 &then &type ...workspace %quad% locked
     &else &return Error %openstat% opening lockfile
   /*------------------------------------------------

   &do /* process DEM
       &set outfile = %quad%_%dem%
       &if not [exists %dest_dir%\%outfile%.tgz -file] &then &do
         /* export to ascii text
         &if not [exists %outfile%.asc] &then gridascii %dem% %outfile%.asc #
         /* archive and compress, switch to unix path convention
         &sys tar --remove-files -zcvf %nix_dir%/%outfile%.tgz %outfile%.asc %outfile%.prj
         &end
/* temp code to clean up intermediate files (before --remove flag was used above)
/*       &if [exists %outfile%.asc -file] &then &do
/*         &sys del %outfile%.asc
/*         &sys del %outfile%.prj
/*         &end
   &end

   &do /* process shaded relief
       &set outfile = %quad%_%shade%
       &if not [exists %dest_dir%\%outfile%.tgz -file] &then &do
         /* export to GeoTiff
         &if not [exists %outfile%.tif] &then &do
            gridimage %shade% gray %outfile% tiff compression
            &end
         /* archive and compress, switch to unix path convention
           /* tar <commands> <destination archive> <infile1> {infile2 ...}
         &sys tar --remove-files -zcvf %nix_dir%/%outfile%.tgz %outfile%.tif %outfile%.tfw
         &end
/* temp code to clean up intermediate files (before --remove flag was used above)
/*       &if [exists %outfile%.tif -file] &then &do
/*         &sys del %outfile%.tif
/*         &sys del %outfile%.tfw
/*         &end
   &end

   /*-------mark workspace not busy-----------------
   &if [close %lockfile%] = 0 &then
     &type ...workspace %quad% unlocked
     &else &return Error closing lockfile
   &if [delete lock -file] <> 0 &then
      &return &error ...Error removing lockfile
   /*-----------------------------------------------
&RETURN
/*---------------------------------------------------------------------------
/* other routines go here

&routine bail
   &if not [variable str] &then &s str Bailing out of %aml$file%...
   &return; &return &warning %str%
/*===========================================================================
/* End Of PKG_250K.aml
/*===========================================================================

