&severity &error &routine bail
/* if there are commandline arguments, make them the TODO list
&args quad tile:REST
&ty   -----------------------------------------------------------------------
&ty   * [upcase %aml$file%] * Project: Updated LIMS 50k DEM * mhw * 02-Aug-2k
&ty     ::Step 2::  (see scripts\__ReadMe.aml)
&ty
&ty     - grab neatlines and create 2km and 100m buffers for each tile
&ty
&ty   -----------------------------------------------------------------------
&if [null %quad%] &then &do
    &s pause [response '<Enter> to continue, something else to abort']
    &if not [null %pause%] &then &return %aml$file% exited by user request.
    &end

&if not [variable .library] &then &return &error Project Environment is not setup. Look for ./scripts/setenv.aml
&workspace %.home%

/* follow ...\<quad>\<tile>\  (...\115h\115h12\)  pattern
/* check for quad placeholder (allows "&r aml_prog . 105d11 105d12 105d13")
&if %quad% = . &then &set quad = [listfile * -directory]
/* check for quad commandline args
&if [null %quad%] &then &set quad = [listfile * -directory]
/* check for tile argument
&if not [null %tile%] &then &set tile_arg = yes

&do quad &list [unquote %quad%]
  &workspace %quad% ; &type ...entering %quad%
    /* check for TODO list on the command line, else TODO = existing workspaces
    &if [null %tile%] &then &set tile = [listfile * -workspace]

        &do tile &list [unquote %tile%]
          &workspace %tile%
            &call main
          &workspace ..
        &end

    /* if tile arg is autogenerated, null out else it won't be generated anew for next loop
    &if not [variable tile_arg] &then &set tile =

  &workspace  ..
&end

&RETURN ...[upcase %aml$fullfile%] finished normally.


/* ------------------------------------------------------------------------
&ROUTINE MAIN
  &if [exists buf_1k -cover] &then &return
    /*-------mark workspace busy----------------------
    &if [exists lock -file] &then &return
    &set lockfile = [OPEN lock openstat -write]
    &if  %openstat% = 0 &then &type ...workspace %tile% locked
        &else &return Error %openstat% opening lockfile
    /*------------------------------------------------
    &describe %.library%\%quad%\%tile%\fneat
    &if %dsc$polygons% = 0  &then build %.library%\%quad%\%tile%\fneat poly

    &type -----{ Making 100m, 1km and 2km buffers for %tile% }-----
        &do bufcov &list buf_2k buf_1k buf_100m
            &if not [exists %bufcov% -cover] &then &do
               &if %bufcov% = buf_2k &then &set bsize = 2000
               &if %bufcov% = buf_1k &then &set bsize = 1000
               &if %bufcov% = buf_100m &then &set bsize = 100
               buffer %.library%\%quad%\%tile%\fneat buf%bsize% # # %bsize% # poly
               rename buf%bsize% %bufcov%
               &end
         &end

    /*-------mark workspace not busy-----------------
    &if [close %lockfile%] = 0 &then
        &type ...workspace %tile% unlocked
        &else &return Error closing lockfile
    &sys del lock
    /*-----------------------------------------------
&RETURN
/* ------------------------------------------------------------------------
&routine bail
   &if not [variable str] &then &s str Bailing out of %aml$file%...
   &return; &return &warning %str%
/*===========================================================================
