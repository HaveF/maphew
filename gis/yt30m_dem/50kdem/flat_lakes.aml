&severity &error &routine bail
/* if there are commandline arguments, make them the TODO list
&args quad tile:REST
&ty --------------------------------------------------------------------------
&ty   * [upcase %aml$file%] * Project: Updated LIMS 50k DEM * mhw * 14-Nov-2k
&ty     ::Step 8.5::  (see scripts\__ReadMe.aml)
&ty
&ty     - replace dem Z with lake Z (make lakes flat)
&ty
&ty     Autorun: &r %aml$file% {quad} {tile(s)}
&ty --------------------------------------------------------------------------
&if [null %quad%] &then &do
    &s pause [response '<Enter> to continue, something else to abort']
    &if not [null %pause%] &then &return %aml$file% exited by user request.
    &end

&if not [variable .library] &then &return &error Project Environment is not setup. Look for ./scripts/setenv.aml
&workspace %.home%

/* put header variables and other semi-global code here
/*&setvar .lake_elev = x:\water\final\nov15-poly
/*&setvar .lake_elev = \h2o-elev\lakes_ply50
&return &error /&*** wait until gerry builds the final lakes cover ***/&

/* follow ...\<quad>\<tile>\  (...\115h\115h12\)  pattern
/* check for quad placeholder (allows "&r aml_prog . 105d11 105d12 105d13")
&if %quad% = . &then &set quad = [listfile * -directory]
/* check for quad commandline args
&if [null %quad%] &then &set quad = [listfile * -directory]
/* check for tile argument
&if not [null %tile%] &then &set tile_arg = yes

&do quad &list [unquote %quad%]
  &workspace %quad%; &type ...entering %quad%
    /* check for TODO list on the command line, else TODO = existing workspaces
    &if [null %tile%] &then &set tile = [listfile * -workspace]

        &do tile &list [unquote %tile%]
            &set make_relief = no
            &call main
            &set make_relief = no
        &end

    /* if tile arg is autogenerated, null out else it won't be generated anew for next loop
    &if not [variable tile_arg] &then &set tile =

  &workspace  ..
&end

&RETURN ...[upcase %aml$fullfile%] finished normally.

/*============================================================================
&ROUTINE MAIN
    &if not [exists %tile%\lake_ctr -cover] &then &return
/*    &if [exists %tile%\dem_flak -grid] &then &return
/*    &if [exists %tile%\flake.done -file] &then &return
   /*-------mark workspace busy----------------------
   &if [exists %tile%\lock -file] &then &return
   &set lockfile = [OPEN %tile%\lock openstat -write]
   &if  %openstat% = 0 &then &type ...workspace %tile% locked
     &else &return Error %openstat% opening lockfile
   /*------------------------------------------------
   &workspace %tile%

   &if [exists flake.done -file] AND [exists dem_flak -grid] &then &goto skip_latrep
      LATTICEREPLACE dem %.lake_elev% dem_flak # Z_metric2
      &set make_relief = yes
      &label skip_latrep

   &call clip_100m

   &if %make_relief% = yes &then &call make_relief

   &sys echo ...lakes have been flattened > flake.done

   &workspace ..
   /*-------mark workspace not busy-----------------
   &if [close %lockfile%] = 0 &then
     &type ...workspace %tile% unlocked
     &else &return Error closing lockfile
   &sys del %tile%\lock
   /*-----------------------------------------------
&RETURN
/*---------------------------------------------------------------------------
/* other routines go here
&routine clip_100m
   &describe buf_100m
   &describe dem_flak
   &set xmin = %dsc$xmin% - %lat$xmin%
   &if %xmin% < 0 &then &set xmin = %xmin% * -1

   /*&if %clip-ext% <> %grid-ext% &then &do
   &if %xmin% > 100 &then &do
       /* clip to 100m boundary poly
       latticeclip dem_flak buf_100m dem_flak_clip
       kill dem_flak all
       rename dem_flak_clip dem_flak
       &type ...Renamed dem_flak_clip to dem_flak
       &end
&return

&routine make_relief
    &if [exists dem_hs -grid] &then &do
      kill dem_hs all
      hillshade dem_flak dem_hs 315  70
      &end
&return

&routine bail
   &if not [variable str] &then &s str Bailing out of %aml$file%...
   &return; &return &warning %str%
/*===========================================================================
/* End Of FLAT_LAKES.aml
/*===========================================================================