&severity &error &routine bail
/* if there are commandline arguments, make them the TODO list
&args centertile
&ty --------------------------------------------------------------------------
&ty   * [upcase %aml$file%] * Project: Updated LIMS 50k DEM * mhw * 11-Dec-2k
&ty     ::Step #::  (see scripts\__ReadMe.aml)
&ty
&ty     - recalcs all DEMs surrounding center tile (including center tile)
&ty     * this deletes and/or overwrites existing data *
&ty
&ty     Autorun: &r %aml$file% {quad} {tile(s)}
&ty --------------------------------------------------------------------------
&if not [variable .library] &then &return &error Project Environment is not setup. Look for ./scripts/setenv.aml

&if [null %centertile%] &then &do
    &return &error *** Center tile must be supplied, eg: &r %aml$file% 105d11
    &s pause [response '<Enter> to continue, something else to abort']
    &if not [null %pause%] &then &return %aml$file% exited by user request.
    &end

&ty
&ty     Current commandline: &r %aml$file% %centertile%
&ty
&set warning = 'This aml will take awhile, are you *sure* the centertile is correct? (y,N)'
&if [getchar %warning% n] = n &then &return Exiting by user request.

&workspace %.home%

/* put header variables and other semi-global code here
&set overlap = overlap.path.bare /* textfile containing paths to surround tiles
&set tiles =
/* identify surrounding to;es
&call ident_surround

   &do tile &list %centertile% [unquote %tiles%]
      &wo [substr %tile% 1 4]
         &call main
      &wo ..
   &end

&RETURN ...[upcase %aml$fullfile%] finished normally.

/*============================================================================
&ROUTINE MAIN
   &if not [exists %tile% -workspace] &then &return
   /*-------mark workspace busy----------------------
   &if [exists %tile%\lock -file] &then &return
   &set lockfile = [OPEN %tile%\lock openstat -write]
   &if  %openstat% = 0 &then &type ...workspace %tile% locked
     &else &return Error %openstat% opening lockfile
   /*------------------------------------------------
   &workspace %tile%

   /* delete existing dem data
   &call clean_ws
   /* create new dem (and associated data)
   &call make_dem

   &workspace ..
   /*-------mark workspace not busy-----------------
   &if [close %lockfile%] = 0 &then
     &type ...workspace %tile% unlocked
     &else &return Error closing lockfile
   &if [delete %tile%\lock -file] <> 0 &then
      &return &error ...Error removing %tile%\lock
   /*-----------------------------------------------
&RETURN
/*---------------------------------------------------------------------------
/* other routines go here
/*---------------------------------------------------------------------------
/* identify which tiles surround the center tile
&routine ident_surround
   &type ...identifying tiles which surround %centertile%
   &wo [substr %centertile% 1 4]\%centertile%; &ty [show workspace]
      &set srcfile = [OPEN %overlap% openstat -read]
         &if  %openstat% <> 0 &then &return Error %openstat% opening %srcfile%
      &set line = [READ %srcfile% readstatus]
         &if %readstatus% <> 0 &then &return &warning Could not read %srcfile%.
      &do &while %readstatus% = 0
         /* --------------------------------------
   /*      &set line = [unquote %line%]     /* looks like z:\arcdata\...\105d11
         &set line = %line%
         &set tile = [substr  %line% 46]  /* grab last 6 characters (105d11)
         &set tiles = %tiles% %tile%      /* append to tile list
         /* --------------------------------------
         /* Read next line.
         &setvar line := [read %srcfile% readstatus]
      &end
      /* Close file.
         &if [close %srcfile%] <> 0 &then &return &warning Unable to close %srcfile%.
   &wo %.home%
&return
/* ---------------------------------------------------------------------------
/* delete all current dems and associated working files
&routine clean_ws
   &type ...cleaning workspace
   /* clean out grids
   &do grd &list dem dem_flak dem_hs
      &if [exists %grd% -grid] &then kill %grd% all
   &end
   /* clean out diagnostic covers
   &do cov &list dem_drain dem_sink
      &if [exists %cov% -grid] &then kill %cov% all
   &end
   /* clean out files
   &do fil &list dem_diag dem_flak_is_100m dem_hs_is_100m dem_is_100m  flake.done [listfile %tile%-dem.* -file] %tile%.dem %tile%.hdr
      &if [exists %fil% -file] &then &s kill [delete %fil% -file]
   &end

   /* kill any parent mosaics in quad dir
   &do grd &list dem_30m dem_90m shade_90m shade_30m
      &if [exists ..\%grd% -grid] &then kill ..\%grd% all
   &end

&return
/* ---------------------------------------------------------------------------
/* ---------------------------------------------------------------------------
&routine make_dem
   &type ...generating new dem
   &set elev = Z_metric
   &set contours = fctr
   &set watercourses = fwtc_f
   &set lake_ctr = lake_ctr   /* lake boundarys as contours
   &set cover_list = %contours% %watercourses% %lake_ctr%
   &set feature = line /* TODO: update for poly covers so we can include boundary and lake polygons
   &set cell-size = 30  /* pixel size in meters
   &set dem = dem       /* the dem name, when in ArcInfo

   &type ___[date -vfull] * Minutes past: [calc [extract 2 [show &pt all]] / 60]

      &set out-dem = %tile%.dem /* anudem ouput dem file

      /* export to ungenerate files
      &run cover2generate %feature% %elev% %cover_list%
      /* build the ANUDEM command file
      &run build_dem_cmd %tile% %cell-size% %elev% %cover_list%
      /* Run Anudem
      &run run_anudem %out-dem% %tile%-dem.cmd anudem.log overwrite %tile%
      /* Convert the dem to ArcInfo grid
      &run adem2grid %out-dem% %dem%

   &type ___[date -vfull] * Minutes past: [calc [extract 2 [show &pt all]] / 60]
&return

&routine bail
   &if not [variable str] &then &s str Bailing out of %aml$file%...
   &return; &return &warning %str%
/*===========================================================================
/* End Of REDO-CENTER.aml
/*===========================================================================