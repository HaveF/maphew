&severity &error &routine bail
/* if there are commandline arguments, make them the TODO list
&args quad tile:REST
&ty --------------------------------------------------------------------------
&ty   * [upcase %aml$file%] * Project: Updated LIMS 50k DEM * mhw * 18-Sept-2k
&ty     ::Step #::  (see scripts\__ReadMe.aml)
&ty
&ty     - copy DEMs and DEM Shades into the repository
&ty     * PLEASE BE CAREEFUL. Study the AML before running.
&ty     * replacement cover|grid is hard coded, edit file before running
&ty
&ty     Autorun: &r %aml$file% {quad} {tile(s)}
&ty --------------------------------------------------------------------------
&ty
&ty     Current commandline: &r %aml$file% %quad% %tile%
&ty
&if not [variable .library] &then &return &error Project Environment is not setup. Look for ./scripts/setenv.aml

&if [null %quad%] &then &do
    &s pause [response '<Enter> to continue, something else to abort']
    &if not [null %pause%] &then &return %aml$file% exited by user request.
    &end

&workspace %.home%
/* ------------ Edit this Section -------------------------------
&s src-type = grid
/*   &s src = dem_hs               /* shaded relief grid
/*   &s dest = fshd                /* name of shade in repository
/*   &s src = dem                  /* digital elevatio model
   &s src = dem_flak             /* dem with flattened lakes
   &s dest = fdem                /* name of dem in repository
&s root = z:\arcdata\50k\base  /* root dir of repository
&s err =                      /* initilize empty error variable
/* ---------------- End of Section ------------------------------
&severity &error &routine problems

/* follow ...\<quad>\<tile>\  (...\115h\115h12\)  pattern
/* check for quad placeholder (allows "&r aml_prog . 105d11 105d12 105d13")
&if %quad% = . &then &set quad = [listfile * -directory]
/* check for quad commandline args
&if [null %quad%] &then &set quad = [listfile * -directory]
/* check for tile argument
&if not [null %tile%] &then &set tile_arg = yes

&do quad &list [unquote %quad%]
  &workspace %quad%; &type ...entering %quad%
    /* check for TODO list on the command line, else TODO = existing workspaces
    &if [null %tile%] &then &set tile = [listfile * -workspace]

        &do tile &list [unquote %tile%]
            &set dest-path = %root%\%quad%\%tile%
            &call main
        &end

    /* if tile arg is autogenerated, null out else it won't be generated anew for next loop
    &if not [variable tile_arg] &then &set tile =

  &workspace  ..
&end

&type ...Replacement errors for tiles: %err%

&RETURN ...[upcase %aml$fullfile%] finished normally.

/*============================================================================
&ROUTINE MAIN
   /* if cover has already been replaced, skip this tile
/*   &if [exists %dest-path%\%dest%.replaced -file] &then &return

   /* if no source grid exists, skip this tile
   &if not [exists %tile%\%src% -%src-type%] &then &return

   /*-------mark workspace busy----------------------
   &if [exists %tile%\lock -file] &then &return
   &set lockfile = [OPEN %tile%\lock openstat -write]
   &if  %openstat% = 0 &then &type ...workspace %tile% locked
     &else &return Error %openstat% opening lockfile
   /*------------------------------------------------
   &workspace %tile%

   &if [exists %src% -grid] &then &do
      /* delete existing repository grid
      &if [exists %dest-path%\%dest% -%src-type%] &then
         kill %dest-path%\%dest% all
      latticeclip %src% buf_100m %dest-path%\%dest%
      &end

   /* verify everything to this point is okay, GRID specific
   &if %src-type% = grid &then &do
      &if [exists %dest-path%\%dest% -%src-type%] &then
         &sys echo ...[upcase %src%] has replaced [upcase %dest-path%\%dest%] >  %dest-path%\%dest%.replaced
         &else &do
            &set err = %err% %tile%
            &type ...%tile% replacement seems to have gone wrong
            &end
   &end

   &workspace ..
   /*-------mark workspace not busy-----------------
   &if [close %lockfile%] = 0 &then
     &type ...workspace %tile% unlocked
     &else &return Error closing lockfile
   &if [delete %tile%\lock -file] <> 0 &then
      &return &error ...Error removing %tile%\lock
   /*-----------------------------------------------
&RETURN
/*---------------------------------------------------------------------------
/* other routines go here
&routine problems
&run beep; &run beep
&ty ***
&s exit = [getchar '*** Problems encountered. Continue?(Y,n)' y]
   &if %exit% = y &then &return
      &else &return ...exiting by user request after problems encountered.
&return
/*---------------------------------------------------------------------------

&routine bail
   &if not [variable str] &then &s str Bailing out of %aml$file%...
   &return; &return &warning %str%
/*===========================================================================
/* End Of DEM2LIB.aml
/*===========================================================================