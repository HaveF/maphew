&severity &error &routine bail
/* if there are commandline arguments, make them the TODO list
&args quad tile:REST
&ty --------------------------------------------------------------------------
&ty   * [upcase %aml$file%] * Project: Updated LIMS 50k DEM * mhw * 18-Sept-2k
&ty   * (Rinse, Lather, Repeat) *
&ty     ::Step #::  (see scripts\__ReadMe.aml)
&ty
&ty     - kill dems and all associated marker files in prep for regenerating
&ty
&ty     Autorun: &r %aml$file% {quad} {tile(s)}
&ty --------------------------------------------------------------------------
&ty
&ty     Current commandline: &r %aml$file% %quad% %tile%
&ty
&if not [variable .library] &then &return &error Project Environment is not setup. Look for ./scripts/setenv.aml

&if [null %quad%] &then &do
    &s pause [response '<Enter> to continue, something else to abort']
    &if not [null %pause%] &then &return %aml$file% exited by user request.
    &end

&workspace %.home%

/* put header variables and other semi-global code here
&set dem = dem
&set shade = dem_hs

/* follow ...\<quad>\<tile>\  (...\115h\115h12\)  pattern
/* check for quad placeholder (allows "&r aml_prog . 105d11 105d12 105d13")
&if %quad% = . &then &set quad = [listfile * -directory]
/* check for quad commandline args
&if [null %quad%] &then &set quad = [listfile * -directory]
/* check for tile argument
&if not [null %tile%] &then &set tile_arg = yes

&do quad &list [unquote %quad%]
  &workspace %quad%; &type ...entering %quad%
    /* check for TODO list on the command line, else TODO = existing workspaces
    &if [null %tile%] &then &set tile = [listfile * -workspace]

        &do tile &list [unquote %tile%]
            &do fer_grid &list %dem%_90m %dem%_30m shade_90m shade_30m
               &if [exists %fer_grid% -grid] &then kill %fer_grid% all
            &end
            &call main
        &end

    /* if tile arg is autogenerated, null out else it won't be generated anew for next loop
    &if not [variable tile_arg] &then &set tile =

  &workspace  ..
&end

&RETURN ...[upcase %aml$fullfile%] finished normally.

/*============================================================================
&ROUTINE MAIN
   /*-------mark workspace busy----------------------
   &if [exists %tile%\lock -file] &then &return
   &set lockfile = [OPEN %tile%\lock openstat -write]
   &if  %openstat% = 0 &then &type ...workspace %tile% locked
     &else &return Error %openstat% opening lockfile
   /*------------------------------------------------
   &workspace %tile%


   &do fer_cover &list %dem%_sink %dem%_drain
      &if [exists %fer_cover% -cover] &then kill %fer_cover% all
   &end

   &do fer_grid &list %dem%_flak %shade% %dem%
      &if [exists %fer_grid% -grid] &then kill %fer_grid% all
   &end

   &do fer_file &list %dem%_is_100m %shade%_is_100m flake.done %dem%_diag dem_flak_is_100m
      &if [exists %fer_file% -file] &then &sys del %fer_file%
   &end


   &workspace ..
   /*-------mark workspace not busy-----------------
   &if [close %lockfile%] = 0 &then
     &type ...workspace %tile% unlocked
     &else &return Error closing lockfile
   &if [delete %tile%\lock -file] <> 0 &then
      &return &error ...Error removing %tile%\lock
   /*-----------------------------------------------
&RETURN
/*---------------------------------------------------------------------------
/* other routines go here

&routine bail
   &if not [variable str] &then &s str Bailing out of %aml$file%...
   &return; &return &warning %str%
/*===========================================================================
/* End Of Rinse, Lather, Repeat
/*===========================================================================
