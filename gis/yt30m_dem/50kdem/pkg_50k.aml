&severity &error &routine bail
/* if there are commandline arguments, make them the TODO list
&args dest_dir quad tile:REST
&ty --------------------------------------------------------------------------
&ty   * [upcase %aml$file%] * Project: Updated LIMS 50k DEM * mhw * 15-Nov-2k
&ty     ::Step 11.5::  (see scripts\__ReadMe.aml)
&ty
&ty     - Package 50k DEMs for distribution
&ty     * GNU Tar must be available on system *
&ty
&ty     Autorun: &r %aml$file% {destination_dir} {quad}
&ty --------------------------------------------------------------------------
&ty
&ty     Current commandline: &r %aml$file% %dest_dir% %quad% %tile%
&ty
&if not [variable .library] &then &return &error Project Environment is not setup. Look for ./scripts/setenv.aml

&if [null %quad%] &then &do
    &s pause [response '<Enter> to continue, something else to abort']
    &if not [null %pause%] &then &return %aml$file% exited by user request.
    &end

&workspace %.home%

/* put header variables and other semi-global code here
/* set the destination directory, make if not exists
&set dest_dir = %.home%\release\50k ; &sys mkdir %dest_dir% > nul
/* build unix version of destination dir for use with Tar & Gzip later
&s nix_dir [subst %dest_dir% \ /]; &s nix_dir [subst %nix_dir% :]; &s nix_dir //%nix_dir%

&set dem = dem       /* source dem grid
&set shade = dem_hs  /* source hillshade grid

/* check for quad commandline args
&if [null %quad%] &then &set quad = [listfile * -directory]

&do quad &list [unquote %quad%]
  &workspace %quad%; &type ...entering %quad%

    &if not [exists %dest_dir%\%quad% -directory] &then ~
      &sys mkdir %dest_dir%\%quad%

    /* check for TODO list on the command line, else TODO = existing workspaces
    &if [null %tile%] &then &set tile = [listfile * -workspace]

        &do tile &list [unquote %tile%]
            &call main
        &end

    /* if tile arg is autogenerated, null out else it won't be generated anew for next loop
    &if not [variable tile_arg] &then &set tile =

  &workspace  ..
&end

&if [locase[show program]] = grid &then QUIT /* exit GRID

&RETURN ...[upcase %aml$fullfile%] finished normally.

/*============================================================================
&ROUTINE MAIN
    /* ------------------------------------------------
    &if [exists %tile%\lock -file] &then &return
    &set lockfile = [OPEN %tile%\lock openstat -write]
    &if  %openstat% = 0 &then &type ...workspace %tile% locked
        &else &return Error %openstat% opening lockfile
    /* ------------------------------------------------
    &workspace %tile%

      &do /* process DEM
          &set outfile = %tile%_%dem%
          &if not [exists %dest_dir%\%quad%\%outfile%.tgz -file] &then &do
            /* export to ascii text
            &if not [exists %outfile%.asc] &then gridascii %dem% %outfile%.asc #
            /* archive and compress, switch to unix path convention
            &sys tar --remove-files -zcvf %nix_dir%/%quad%/%outfile%.tgz %outfile%.asc %outfile%.prj
            &end
      &end

      &do /* process shaded relief
          &set outfile = %tile%_%shade%
          &if not [exists %dest_dir%\%quad%\%outfile%.tgz -file] &then &do
            /* export to GeoTiff
            &if not [exists %outfile%.tif] &then &do
               gridimage %shade% gray %outfile% tiff compression
               &end
            /* archive and compress, switch to unix path convention
              /* tar <commands> <destination archive> <infile1> {infile2 ...}
            &sys tar --remove-files -zcvf %nix_dir%/%quad%/%outfile%.tgz %outfile%.tif %outfile%.tfw
            &end
      &end

    &workspace ..
    /*--------------------------------
    &if [close %lockfile%] = 0 &then
        &type ...workspace %tile% unlocked
        &else &return Error closing lockfile
    &sys del %tile%\lock
    /*--------------------------------
/*&stop /*debug break point
&RETURN
/*---------------------------------------------------------------------------
/* other routines go here

&routine bail
   &if not [variable str] &then &s str Bailing out of %aml$file%...
   &return; &return &warning %str%
/*===========================================================================
/* End Of PKG_50K.aml
/*===========================================================================

